plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
}

checkstyle {
    toolVersion = "8.5"
}

pmd {
    consoleOutput = true
    toolVersion = "6.44.0"
    ruleSetFiles = files("config/pmd-ruleset.xml")
    ruleSets = []
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.runelite.net'
    }
    mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
    implementation 'org.tomlj:tomlj:1.1.1'
    testImplementation 'org.tomlj:tomlj:1.1.1'

    compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:5.1.1'
    testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion
}

group = 'melky.resourcepacks'
version = '1.7.3'

tasks.withType(Pmd) {
    enabled gradle.startParameter.taskNames.contains('pmdMain')
}

tasks.withType(Checkstyle) {
    enabled gradle.startParameter.taskNames.contains('checkstyleMain')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release.set(11)
}

tasks.withType(Jar) {
    exclude 'org/tomlj/TomlCommand.class'
    exclude 'org/antlr/v4/runtime/misc/TestRig.class'
}

test {
    systemProperty('spriteFolder', System.getProperty('spriteFolder'))
    systemProperty('packFolder', System.getProperty('packFolder'))
    systemProperty('sampleOutput', System.getProperty('sampleOutput'))
    systemProperty('sampleMinifiedOutput', System.getProperty('sampleMinifiedOutput'))
}
